autoload colors; colors;
export LSCOLORS="Gxfxcxdxbxegedabagacad"
setopt prompt_subst
 
# prompt constants 
ZSH_THEME_SVN_PROMPT_PREFIX="%{$reset_color%}%{$fg[green]%}[svn:"
ZSH_THEME_GIT_PROMPT_PREFIX="%{$reset_color%}%{$fg[green]%}[git:"
ZSH_THEME_SVN_PROMPT_SUFFIX="]%{$reset_color%}"
ZSH_THEME_GIT_PROMPT_SUFFIX="]%{$reset_color%}"
ZSH_THEME_PROMPT_DIRTY="%{$fg[red]%}✖%{$reset_color%} "
ZSH_THEME_PROMPT_CLEAN="%{$fg[green]%}✔%{$reset_color%} "
 
# show git branch/tag, or name-rev if on detached head
parse_git_branch() {
  (command git symbolic-ref -q HEAD || command git name-rev --name-only --no-undefined --always HEAD) 2>/dev/null
}

parse_changes() {
  behind=$(git log --oneline HEAD..origin/master | wc -l | tr -d " ")
  ahead=$(git log --oneline origin/master..HEAD | wc -l | tr -d " ")
  echo " ↑$ahead↓$behind"
}
 
# show dirty or clean icon
parse_git_dirty() {
  if command git diff-index --quiet HEAD 2> /dev/null; then
    echo "$ZSH_THEME_PROMPT_CLEAN"
  else
    echo "$ZSH_THEME_PROMPT_DIRTY"
  fi
}

# show svn branch
parse_svn_branch() {
  if [[ -d .svn ]]; then
    svn_info=$(svn info --xml 2>/dev/null)
    svn_root=$(echo "${svn_info}" | grep "<root>" | sed 's/<[^>]*>//g')
    svn_url=$(echo "${svn_info}" | grep "<url>"| sed 's/<[^>]*>//g')
    echo "${svn_url#${svn_root}}"
  fi
}

# show dirty or clean icon
parse_svn_dirty() {
  s=$(svn status 2>/dev/null |grep -E '^\s*[ACDIM!?L]' 2>/dev/null)
  if [ $s ]; then 
    echo "$ZSH_THEME_PROMPT_DIRTY"
  else 
    echo "$ZSH_THEME_PROMPT_CLEAN"
  fi
}
 
# if in a git repo, show dirty indicator + git branch
custom_status() {
  local git_where="$(parse_git_branch)"
  local svn_where="$(parse_svn_branch)"
  [ -n "$git_where" ] && echo "$(parse_git_dirty)$ZSH_THEME_GIT_PROMPT_PREFIX${git_where#(refs/heads/|tags/)}$(parse_changes)$ZSH_THEME_GIT_PROMPT_SUFFIX"
  [ -n "$svn_where" ] && echo "$(parse_svn_dirty)$ZSH_THEME_SVN_PROMPT_PREFIX${${svn_where#*/branches}[(ws:/:)1]}$ZSH_THEME_SVN_PROMPT_SUFFIX"
}
 
RPS1='$(custom_status) $EPS1'
 
# basic prompt on the left
PROMPT='%{$fg[cyan]%}%n@%~% %(?.%{$fg[green]%}.%{$fg[red]%}) %B→%b%{$reset_color%} '